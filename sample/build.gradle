apply plugin: 'com.android.application'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.mindcoders.phial.sample"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "distribution"

    productFlavors {
        qa {
            buildConfigField "long", "BUILD_TIMESTAMP", "${new Date().time}L"
            buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""

            dimension "distribution"
        }

        prod {
            dimension "distribution"
        }
    }
}
dependencies {
    implementation "com.android.support:appcompat-v7:26.1.0"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.jakewharton.timber:timber:4.5.1'

    qaImplementation project(':phial-overlay')
    qaImplementation project(':phial-logging')
    qaImplementation project(':phial-jira')
    qaImplementation project(':phial-key-value')
    prodImplementation project(':phial-key-value')
}
