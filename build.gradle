// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
    project.ext {
        GIT_HUB_GROUP = 'com.github.roshakorost'
    }


}

subprojects {
    apply plugin: 'checkstyle'
    project.checkstyle {
        toolVersion "7.6.1"
        configFile rootProject.file('checkstyle.xml')
    }

    task checkstyle(type: Checkstyle) {
        showViolations = true
        configFile rootProject.file('checkstyle.xml')

        source 'src'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'

        // empty classpath
        classpath = files()
    }
}

subprojects.each { project ->
    def checkStyleTask = project.getTasksByName("checkstyle", false).find()
    def checkTask = project.getTasksByName("check", false).find()
    if (checkTask != null && checkStyleTask != null) {
        checkTask.dependsOn checkStyleTask
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
