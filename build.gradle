// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
        classpath 'me.tatarka:gradle-retrolambda:3.7.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

subprojects {
    apply plugin: 'checkstyle'
    project.checkstyle {
        toolVersion "8.4"
        configFile rootProject.file('checkstyle.xml')
        configProperties = [ "suppressionFile" : rootProject.file('suppressions.xml')]
    }

    task checkstyle(type: Checkstyle) {
        showViolations = true
        configFile rootProject.file('checkstyle.xml')
        configProperties = [ "suppressionFile" : rootProject.file('suppressions.xml')]

        source 'src'
        include '**/*.java'
        exclude '**/gen/**'
        exclude '**/R.java'
        exclude '**/BuildConfig.java'

        // empty classpath
        classpath = files()
    }
}

configure(subprojects.findAll { it.name != 'sample' }) {
    apply plugin: 'com.github.dcendents.android-maven'
    group = 'com.github.roshakorost'
}

subprojects.each { project ->
    def checkStyleTask = project.getTasksByName("checkstyle", false).find()
    def checkTask = project.getTasksByName("check", false).find()
    if (checkTask != null && checkStyleTask != null) {
        checkTask.dependsOn checkStyleTask
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
